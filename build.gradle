buildscript {
    repositories {
        maven { url("https://files.minecraftforge.net/maven") }
        maven { url("https://libraries.minecraft.net") }
        maven { url("https://plugins.gradle.org/m2/") }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(
            group: "net.minecraftforge.gradle",
            name: "ForgeGradle",
            version: forgeGradleVersion,
            changing: true
        )
        classpath(
            "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        )
        classpath(
            "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        )
    }
}

apply(plugin: "org.jetbrains.dokka")
apply(plugin: "net.minecraftforge.gradle")
apply(plugin: "kotlin")
apply(plugin: "java")

version = projectEssentialsVersion
group = "com.mairwunnx.$projectEssentialsId"
archivesBaseName = projectEssentialsName

configurations {
    embed
    compile.extendsFrom(embed)
}

minecraft {
    mappings(
        channel: mappingsChannelType,
        version: mappingsChannelVersion
    )

    runs {
        client {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(devRunClientPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentialsCore {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }

        server {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(devRunServerPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentialsCore {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url("https://minecraft.curseforge.com/api/maven/") }
    maven { url("https://libraries.minecraft.net") }
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: forgeBaseVersion)
    compile(group: "com.mojang", name: "brigadier", version: brigadierBaseVersion)
    compile(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinxSerializationVersion
    )
    embed(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinxSerializationVersion
    )
    compile(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlinJdkVersionTarget",
        version: kotlinVersion
    )
    embed(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlinJdkVersionTarget",
        version: kotlinVersion
    )
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/libs/docs"
}

project.tasks["jar"].dependsOn(project.tasks["dokka"])

task fatJar(type: Jar) {
    manifest {
        attributes([
            "Specification-Title"     : "$projectEssentialsName-MOD",
            "Specification-Vendor"    : projectEssentialsVendor,
            "Specification-Version"   : projectEssentialsVersion,
            "Implementation-Title"    : "$projectEssentialsName-MOD",
            "Implementation-Version"  : projectEssentialsVersion,
            "Implementation-Vendor"   : projectEssentialsVendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    baseName = project.name + '-mod'
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    with jar
}

project.tasks["jar"].dependsOn(project.tasks["fatJar"])

jar {
    manifest {
        attributes([
            "Specification-Title"     : "$projectEssentialsName-API",
            "Specification-Vendor"    : projectEssentialsVendor,
            "Specification-Version"   : projectEssentialsVersion,
            "Implementation-Title"    : "$projectEssentialsName-API",
            "Implementation-Version"  : projectEssentialsVersion,
            "Implementation-Vendor"   : projectEssentialsVendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    baseName = project.name + '-api'
}

sourceCompatibility = targetCompatibility =
    compileJava.sourceCompatibility =
        compileJava.targetCompatibility = jvmVersionTarget

compileKotlin.kotlinOptions.jvmTarget =
    compileTestKotlin.kotlinOptions.jvmTarget = jvmVersionTarget
